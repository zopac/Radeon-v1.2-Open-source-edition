buildscript {
    repositories {
		maven { url = 'http://files.minecraftforge.net/maven' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
		maven { url = 'https://libraries.minecraft.net' }
		maven { url = 'https://plugins.gradle.org/m2' }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version project.modVersion
group project.modGroup

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
	options.encoding = 'UTF-8'
}

minecraft {
	version = project.forgeVersion
	runDir = 'run'
	mappings = project.mcpVersion
	coreMod = 'me.wolfsurge.mixin.MixinLoader'
	makeObfSourceJar = false
}

repositories {
    mavenCentral()

	maven {
		name = 'spongepowered-repo'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}
	maven {
		name = 'swt-repo'
		url = "http://maven-eclipse.github.io/maven"
	}
	maven {
		name = "jitpack.io"
		url = "https://jitpack.io"
	}
}

dependencies {
	compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
		exclude module: 'commons-io'
	}
	compile "com.mojang:brigadier:1.0.17"
	compile "com.moandjiezana.toml:toml4j:0.7.2"
	compile "com.github.ZeroMemes:Alpine:1.5"
	compile group: 'net.jodah', name: 'typetools', version: '0.5.0'
	compile group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
	compile group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'
	compile fileTree(dir: 'libraries', include: '*.jar')
}

processResources {
	inputs.property 'version', project.version
	inputs.property 'mcversion', project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
	dependencies {
		include(dependency('org.spongepowered:mixin'))
		include(dependency('com.github.ZeroMemes:Alpine'))
		include(dependency('net.jodah:typetools'))
		include(dependency('com.googlecode.json-simple:json-simple:1.1.1'))
		include(dependency('club.minnced:java-discord-rpc:2.0.1'))
		include(dependency('com.mojang:brigadier:1.0.17'))
		include(dependency('com.moandjiezana.toml:toml4j:0.7.2'))
	}
	exclude 'LICENSE.txt'
	classifier = 'release'
}

mixin {
	defaultObfuscationEnv searge
	add sourceSets.main, 'mixins.radeon.refmap.json'
}

reobf {
	shadowJar {
		mappingType = 'SEARGE'
		classpath = sourceSets.main.compileClasspath
	}
}

jar {
	manifest {
		attributes(
				'MixinConfigs': 'mixins.radeon.json',
				'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
				'TweakOrder': 0,
				'FMLCorePluginContainsFMLMod': 'true',
				'FMLCorePlugin': 'me.wolfsurge.mixin.MixinLoader',
				'ForceLoadAsMod': 'true',
				'FMLAT': 'radeon_at.cfg'
		)
	}
}

build.dependsOn(shadowJar)

// Swap between these two for building / developing, because of the 'Asset Problem' in IntelliJ (also delete the build folder)

//sourceSets { main { output.resourcesDir = output.classesDir } } // Developing
sourceSets { sourceSets -> main } // Building